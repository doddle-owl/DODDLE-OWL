plugins {
    id 'java'
    id 'application'
    id 'com.jfrog.bintray' version '1.8.4'
    id "org.beryx.runtime" version "1.12.0"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'maven-publish'
    id 'idea'
}

group 'org.doddle-owl'
version = '20.12.1'
mainClassName = 'org.doddle_owl.DODDLE_OWL'
sourceCompatibility = targetCompatibility = '11'

project.ext {
    artifactId = 'doddle-owl'
    defaultEncoding = 'UTF-8'
}

repositories {
    jcenter()
}

wrapper {
    gradleVersion = '6.7.1'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'org.doddle_owl.DODDLE_OWL'
    }
}

idea {
    module {
        inheritOutputDirs = true
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.desktop', 'java.logging', 'java.xml', 'java.sql']
    imageDir = file("$buildDir/doddle-owl-${version}")
    imageZip = file("$buildDir/doddle-owl-${version}.zip")

    jpackage {
        // Windows and macOS
        outputDir = 'my-packaging'
        mainJar = "doddle-owl-${version}.jar"
        imageName = "doddle-owl-${version}"
        skipInstaller = false
        installerName = "doddle-owl"
        // macOS
//        jpackageHome = '/Library/Java/JavaVirtualMachines/jdk-14.0.2.jdk/Contents/Home'
//        imageOptions = ['--verbose', '--icon', './src/main/resources/images/doddle-owl.icns']
        // Windows
        jpackageHome = 'C:/Program Files/Java/jdk-14.0.2'
        installerType = 'msi'
        imageOptions = ['--icon', './src/main/resources/images/doddle-owl.ico']
        installerOptions = ['--vendor', 'Takeshi Morita', '--win-shortcut']
    }

}

tasks.runtime.doLast {
    copy {
        from('./README.md')
        into("$buildDir/doddle-owl-${version}")
    }
    copy {
        from('./README.ja.md')
        into("$buildDir/doddle-owl-${version}")
    }
    copy {
        from('./LICENSE')
        into("$buildDir/doddle-owl-${version}")
    }
    copy {
        from('./src/main/resources/jpwn_dict_1.1/')
        into("$buildDir/doddle-owl-${version}/ontologies/jpwn_dict_1.1/")
    }
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'org.doddle_owl.DODDLE_OWL'
    }
    archiveFileName = "doddle-owl-${version}.jar"
    exclude 'jpwn_dict_1.1/'
}

dependencies {
    compile 'com.formdev:flatlaf:0.45'
    compile 'org.xerial:sqlite-jdbc:3.34.0'
    compile 'commons-cli:commons-cli:1.4'
    compile 'org.apache.pdfbox:pdfbox:2.0.19'
    compile 'net.sf.extjwnl:extjwnl:2.0.3'
    compile 'net.sf.extjwnl:extjwnl-data-wn31:1.2'
    compile 'com.atilika.kuromoji:kuromoji-ipadic:0.9.0'
    compile 'org.apache.poi:poi-ooxml:3.7'
    compile 'edu.stanford.nlp:stanford-parser:3.9.2'
    compile 'org.apache.jena:apache-jena-libs:3.17.0'
    compile 'org.slf4j:slf4j-simple:1.7.30'
    compile 'log4j:log4j:1.2.17'
    compile 'com.optimaize.languagedetector:language-detector:0.6'
    compileOnly 'org.projectlombok:lombok:1.18.16'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId = 'org.doddle-owl'
            artifactId = 'doddle-owl'

            from components.java
            pom {
                name = 'DODDLE-OWL'
                description = 'a Domain Ontology rapiD DeveLopment Environment - OWL extension'
                url = 'http://doddle-owl.org/'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE, Version 3'
                        url = 'http://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 't-morita'
                        name = 'Takeshi Morita'
                        email = 'morita@it.aoyama.ac.jp'
                    }
                }
            }
        }
    }
}

def siteUrl = 'https://doddle-owl.org/'
def githubUrl = 'https://github.com/doddle-owl/doddle-owl'
bintray {
    user = project.getProperty('bintray_user')
    key = project.getProperty('bintray_api_key')
    publications = ['MyPublication']
    override = true
    pkg {
        repo = 'maven'
        name = project.group + ':' + project.ext.artifactId
        userOrg = 'doddle-owl'
        licenses = ['GPL']
        websiteUrl = siteUrl
        vcsUrl = githubUrl + '.git'
        issueTrackerUrl = githubUrl + '/issues'
        version {
            name = project.version
        }
    }
}
