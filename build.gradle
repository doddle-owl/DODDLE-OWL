plugins {
    id "com.github.johnrengelman.shadow" version "4.0.3"
    id 'org.beryx.jlink' version '2.2.1'
    id 'application'
    id 'maven-publish'
    id 'idea'
}

group 'org.doddle-owl'
version = '1.0RC3'
mainClassName = 'org.doddle_owl.DODDLE_OWL'
sourceCompatibility = targetCompatibility = '11'

project.ext {
    artifactId = 'doddle-owl'
    defaultEncoding = 'UTF-8'
}

repositories {
    jcenter()
    maven {
        url 'http://mrcube.org/mvn_repo/'
    }
    maven {
        url 'http://jfontchooser.sourceforge.jp/repository/'
    }
}

wrapper {
    gradleVersion = '5.0'
}

idea{
    module{
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

jar {
    into('org/doddle_owl/resources') {
        from 'src/main/java/org/doddle_owl/resources'
    }
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'org.doddle_owl.DODDLE_OWL'
    }
    archiveName = "doddle-owl.jar"
}

jlink {
    mergedModule {
        requires 'java.desktop'
    }
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'doddle-owl'
    }
}

dependencies {
    compile 'org.xerial:sqlite-jdbc:3.25.2'
    compile 'commons-cli:commons-cli:1.4'
    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.pdfbox:pdfbox:2.0.13'
    compileOnly 'org.projectlombok:lombok:1.18.4'
    compile 'net.sf.extjwnl:extjwnl:2.0.0'
    compile 'net.sf.extjwnl:extjwnl-data-wn31:1.2'
    compile 'com.google.code:lucene-gosen-ipadic:1.2.1'
    compile 'org.apache.poi:poi-ooxml:3.7'
    compile 'edu.stanford.nlp:stanford-parser:3.5.1'
    compile 'org.apache.jena:jena-tdb:3.9.0'
    compile 'org.mrcube:mrcube:1.0.3'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:slf4j-log4j12:1.7.5'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    testCompile 'org.junit.platform:junit-platform-launcher:1.3.2'
    testCompile 'org.junit.platform:junit-platform-runner:1.3.2'
}

task zipApp(type: Zip, dependsOn: startShadowScripts) {
    archiveName = "doddle-owl-${version}.${extension}"

    from('./README.md')
    from('./README.ja.md')
    from('./src/main/resources/gpl.md')
    into('bin') {
        from('build/scriptsShadow/doddle-owl')
        from('build/scriptsShadow/doddle-owl.bat')
    }

    into('lib') {
        from('build/libs/doddle-owl.jar')
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.doddle-owl'
            artifactId = 'doddle-owl'
            version = '1.0RC3'

            from components.java
        }
    }
}

defaultTasks 'zipApp'
